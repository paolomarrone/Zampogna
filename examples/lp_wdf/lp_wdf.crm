pi = 3.141592653589793

b, R0 = wdf_resistor(a, R) {
	b = 0
	R0 = R
}

b, R0 = wdf_capacitor(a, C) {
	b = delay1(a)
	R0 = 0.5 / (C * fs)
}

b = wdf_voltage_source_root(a, E) {
	b = 2 * E - a
}

bu, bl, br, R0 = wdf_3port_series(au, al, ar, Rl, Rr) {
	bl = al - Rl / (Rl + Rr) * (al + ar + au)
	br = ar - Rr / (Rl + Rr) * (al + ar + au)
	bu = -(al + ar)
	R0 = Rl + Rr
}

y = lp_filter(x, cutoff) {
	fc = (0.1 + 0.3 * cutoff) * fs
	C = 1e-6
	R = 1 / (2 * pi * fc * C)

	bR, RR = wdf_resistor(aR, R)
	bC, RC = wdf_capacitor(aC, C)
	bV = wdf_voltage_source_root(aV, x)
	aV, aR, aC, Rp = wdf_3port_series(bV, bR, bC, RR, RC)
	@aC = 0
	
	y = 0.5 * (aC + bC)
}

y = lp_filter3 (x, cutoff, vol) {
	y = lp_filter(lp_filter(lp_filter(x, cutoff), cutoff), cutoff) * vol
}